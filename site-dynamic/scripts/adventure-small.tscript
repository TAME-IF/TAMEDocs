module
{
	title = "A Small Adventure";
	author = "Matthew Tropiano";
	email = "email@somesite.com";
}

action general a_quit named "quit";
action general a_look named "look around", "look", "l";
action transitive a_take named "take", "pick up", "t", "p";
action transitive a_examine named "look at", "examine", "x";
action transitive a_open named "open", "o";
action ditransitive a_use named "use" uses conjunctions "with", "and";

// The KEY!
object o_key named "key", "small key"
{
	onAction(a_examine)
	{
		Textln("Looks like a small key.");
	}
	
	onAction(a_take)
	{
		if (!hasObject(player, this))
		{
			giveObject(player, this);
			Textln("You take the key.");
		}
		else
			Textln("You already have this.");
	}
	
	onRoomBrowse()
	{
		Textln("There's a key, here.");
	}
	
	onPlayerBrowse()
	{
		Textln("A key.");
	}
	
}

// The DOOR!
object o_door named "door" uses determiners "the"
{
	init()
	{
		locked = true;
	}

	onAction(a_examine)
	{
		Textln("Looks like a wooden door.");
		addObjectName(this, "wooden door");
	}
	
	onActionWith(a_use, o_key)
	{
		if (locked)
		{
			locked = false;
			Textln("You unlocked the door!");
		}
		else
		{
			Textln("The door is already unlocked.");
		}
	}
	
	onAction(a_take)
	{
		Textln("You can't pick up a door.");
	}
	
	onAction(a_open)
	{
		queue a_use, this;
	}
	
	onAction(a_use)
	{
		if (locked)
			Textln("The door is locked.");
		else
		{
			Textln("You open the door.");
			world.win = true;
		}
	}

	onRoomBrowse()
	{
		Textln("There's a wooden door, here.");
	}

}

// YOU!
player p_guy
{
	function oops()
	{
		local x = irandom(3);
		if (x == 0)
			Textln("I can't do that.");
		else if (x == 1)
			Textln("I don't understand.");
		else
			Textln("I don't think so.");
	}
	
	onUnhandledAction()
	{
		oops();
	}
	
	onMalformedCommand()
	{
		oops();
	}
	
	onUnknownCommand()
	{
		oops();
	}
	
	onIncompleteCommand()
	{
		oops();
	}

}

// The ROOM!
room r_startroom
{
	init()
	{
		giveObject(r_startroom, o_key);
		giveObject(r_startroom, o_door);
	}
}

// HELLO, WORLD!
world
{
	init()
	{
		win = false;
	}

	afterSuccessfulCommand()
	{
		if (win == true)
		{
			Textln("You escaped the room!");
			quit;
		}
		else if (objectCount(player) > 0)
		{
			Textln("You have:");
			Browse(player);
		}
	}
	
	start()
	{
		setRoom(p_guy, r_startroom);
		setPlayer(p_guy);

		Textln("Escape the room!");
		Browse(room);
	}
}
