action modal a_go named "go" uses modes "north", "south", "west", "east";
action transitive a_examine named "look at", "examine", "x"; 
action general a_quit named "quit"; 

action general a_look; // queued every action.
action general a_roomcheck; // queued every action.

room r_northroom;
room r_southroom;
room r_westroom;
room r_eastroom;

room r_centerroom
{
	onAction(a_look)
	{
		textln("You are in the center room.");
		textln("Ways out are north, south, east, or west.");
	}

	onModalAction(a_go, "north")
	{
		setRoom(player, r_northroom);
	}

	onModalAction(a_go, "south")
	{
		setRoom(player, r_southroom);
	}

	onModalAction(a_go, "west")
	{
		setRoom(player, r_westroom);
	}

	onModalAction(a_go, "east")
	{
		setRoom(player, r_eastroom);
	}
}

extend room r_northroom
{
	onAction(a_look)
	{
		textln("You are in the north room.");
		textln("The way out is south.");
	}

	onModalAction(a_go, "south")
	{
		setRoom(player, r_centerroom);
	}
}

extend room r_southroom
{
	onAction(a_look)
	{
		textln("You are in the south room.");
		textln("The way out is north.");
	}

	onModalAction(a_go, "north")
	{
		setRoom(player, r_centerroom);
	}
}

extend room r_westroom
{
	onAction(a_look)
	{
		textln("You are in the west room.");
		textln("The way out is east.");
	}

	onModalAction(a_go, "east")
	{
		setRoom(player, r_centerroom);
	}
}

extend room r_eastroom
{
	onAction(a_look)
	{
		textln("You are in the east room.");
		textln("The way out is west.");
	}

	onModalAction(a_go, "west")
	{
		setRoom(player, r_centerroom);
	}
}

player p_main
{
	onAction(a_roomcheck)
	{
		if (currentRoomIs(this, r_centerroom))
			textln("Whoa. This is my favorite room!");
	}

	onIncompleteCommand(a_go)
	{
		textln("GO where? (Need a compass direction)");
	}

	onUnhandledAction(a_go)
	{
		textln("Can't go that way.");
	}
}

world
{
	init()
	{
		setRoom(p_main, r_centerroom);
		setPlayer(p_main); // from this moment on, this is the "player" keyword.
	}

	start()
	{
		textln("Type \"quit\" to quit, \"go\" and a compass direction to move between rooms.");
		queue a_look;
	}

	onAction(a_quit)
	{
		quit;
	}

	afterSuccessfulCommand()
	{
		queue a_look;
		queue a_roomcheck;
	}
}
