action modal a_go named "go" uses modes "north", "south", "west", "east";
action transitive a_examine named "look at", "examine", "x"; 
action general a_quit named "quit"; 

action general a_look; // queued every action.
action general a_objectcheck; // queued every action.

// Room Prototypes
room r_northroom;
room r_southroom;

object o_redball named "red ball", "ball" uses determiners "the"
{
	onAction(a_examine)
	{
		Textln("It's a red ball.");
	}

	onRoomBrowse()
	{
		Textln("A red ball is here.");
	}
}

object o_blueball named "blue ball", "ball" uses determiners "the"
{
	onAction(a_examine)
	{
		Textln("It's a blue ball.");
	}

	onRoomBrowse()
	{
		Textln("A blue ball is here.");
	}
}

object o_greenball named "green ball", "ball" uses determiners "the"
{
	onAction(a_examine)
	{
		Textln("It's a green ball.");
	}

	onPlayerBrowse()
	{
		Textln("A green ball.");
	}
}

extend room r_northroom
{
	onAction(a_look)
	{
		Textln("You are in the north room.");
		Textln("The way out is south.");
		Browse(room);
	}

	onModalAction(a_go, "south")
	{
		SetRoom(player, r_southroom);
	}
}

extend room r_southroom
{
	onAction(a_look)
	{
		Textln("You are in the south room.");
		Textln("The way out is north.");
		Browse(room);
	}

	onModalAction(a_go, "north")
	{
		SetRoom(player, r_northroom);
	}
}

player p_main
{
	onIncompleteCommand(a_go)
	{
		Textln("GO where? (Need a compass direction)");
	}

	onUnhandledAction(a_go)
	{
		Textln("Can't go that way.");
	}
}

world
{
	init()
	{
		SetRoom(p_main, r_southroom);
		SetPlayer(p_main); // from this moment on, this is the "player" keyword.
		GiveObject(p_main, o_greenball);
		GiveObject(world, o_redball);
		GiveObject(r_northroom, o_blueball);
	}

	start()
	{
		Textln("Type \"quit\" to quit, \"go\" and a compass direction to move between rooms.");
		Textln("You have a green ball.");
		queue a_look;
		queue a_objectcheck;
	}

	onAction(a_objectcheck)
	{
		if (PlayerCanAccessObject(player, o_greenball))
			Textln("You can access the green ball.");
		else
			Textln("You can NOT access the green ball.");

		if (PlayerCanAccessObject(player, o_redball))
			Textln("You can access the red ball.");
		else
			Textln("You can NOT access the red ball.");

		if (PlayerCanAccessObject(player, o_blueball))
			Textln("You can access the blue ball.");
		else
			Textln("You can NOT access the blue ball.");
	}

	onAction(a_quit)
	{
		quit;
	}

	afterSuccessfulCommand()
	{
		queue a_look;
		queue a_objectcheck;
	}
}
