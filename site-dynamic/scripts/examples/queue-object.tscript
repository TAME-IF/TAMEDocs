action modal a_move named "move", "go" uses modes "north", "south", "west", "east";
action general a_look named "look", "look around", "look at room";
action general a_quit named "quit";
action transitive a_examine named "examine", "look at";
action transitive a_use named "use";

// The ORB!
object o_orb named "orb"
{
	init()
	{
		// Pick a room.
		roomid = IRandom(4) + 1;
	}

	function isCorrectRoom()
	{
		return room.id == roomid;
	}
	
	onAction(a_examine)
	{
		if (isCorrectRoom())
			Textln("The orb is starting to glow!");
		else
			Textln("The orb is doing nothing.");
	}

	onAction(a_use)
	{
		if (isCorrectRoom())
		{
			Textln("You teleport out of the adventure!");
			quit;
		}
		else
			Textln("Nothing happens. Must not be the correct room.");
	}
}

// Declare rooms here because we'll need to reference them immediately in r_center.
room r_south;
room r_north;
room r_east;
room r_west;

room r_center
{
	init()
	{
		id = 0;
	}

	onAction(a_look)
	{
		Textln("Looks like I'm in the center room.");
	}

	onModalAction(a_move, "north")
	{
		SetRoom(player, r_north);
	}

	onModalAction(a_move, "south")
	{
		SetRoom(player, r_south);
	}

	onModalAction(a_move, "west")
	{
		SetRoom(player, r_west);
	}

	onModalAction(a_move, "east")
	{
		SetRoom(player, r_east);
	}
}

// Filling out the rooms here.
extend room r_south
{
	init()
	{
		id = 1;
	}

	onAction(a_look)
	{
		Textln("Looks like I'm in the south room.");
	}

	onModalAction(a_move, "north")
	{
		SetRoom(player, r_center);
	}
}

extend room r_north
{
	init()
	{
		id = 2;
	}

	onAction(a_look)
	{
		Textln("Looks like I'm in the north room.");
	}

	onModalAction(a_move, "south")
	{
		SetRoom(player, r_center);
	}
}

extend room r_east
{
	init()
	{
		id = 3;
	}

	onAction(a_look)
	{
		Textln("Looks like I'm in the east room.");
	}

	onModalAction(a_move, "west")
	{
		SetRoom(player, r_center);
	}
}

extend room r_west
{
	init()
	{
		id = 4;
	}

	onAction(a_look)
	{
		Textln("Looks like I'm in the west room.");
	}

	onModalAction(a_move, "east")
	{
		SetRoom(player, r_center);
	}
}

// Our intrepid... hero?
player p_guy
{
	onUnhandledAction()
	{
		Textln("Can't do that.");
	}

	onMalformedCommand(a_move)
	{
		Textln("That is not a valid direction to travel! (north, south, east, west)");
	}

	onMalformedCommand()
	{
		Textln("Can't do that.");
	}
	
	onIncompleteCommand(a_move)
	{
		Textln("Need a direction to go.");
	}
	
	onIncompleteCommand()
	{
		Textln("You should probably complete that thought.");
	}

}

world
{
	onAction(a_quit)
	{
		Textln("Quitting.");
		quit;
	}

	start()
	{
		// init player.
		SetPlayer(p_guy);
		SetRoom(p_guy, r_center);
		GiveObject(player, o_orb);

		// Kick us off. afterSuccessfulCommand() is not called after start().
		queue a_look;
		queue a_examine, o_orb;

		Textln("The ORB that you are carrying reacts to a certain room.");
		Textln("Using the ORB in this state will help you escape.");
		Textln("");
		Textln("Type \"move\" or \"go\" and a compass direction.");
		Textln("Type \"quit\" to quit.");
	}

	afterSuccessfulCommand()
	{
		queue a_look;
		queue a_examine, o_orb;
	}
	
}
